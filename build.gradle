buildscript {
    repositories {
        mavenLocal()
        jcenter()
        maven {
            url 'http://repo.enonic.com/public'
        }
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }

    dependencies {
        classpath "com.enonic.xp:gradle-plugin:${xpVersion}"
    }
}

plugins {
    id "com.moowork.node" version "0.13"
    id "com.moowork.gulp" version "0.13"
}

apply plugin: 'maven'
apply plugin: 'com.enonic.xp.app'
apply plugin: 'com.moowork.gulp'

app {
    name = project.appName
    displayName = project.displayName
    vendorName = 'Enonic AS'
    vendorUrl = 'http://enonic.com'
}

dependencies {
    compile "com.enonic.xp:core-api:${xpVersion}"
    compile "com.enonic.xp:jaxrs-api:${xpVersion}"
    compile "com.enonic.xp:portal-api:${xpVersion}"
    include "com.enonic.xp:lib-content:${xpVersion}"
    include "com.enonic.xp:lib-portal:${xpVersion}"
    include "com.enonic.xp:lib-thymeleaf:${xpVersion}"
    include "com.enonic.xp:lib-xslt:${xpVersion}"
    include "com.enonic.xp:lib-mustache:${xpVersion}"
    include "com.enonic.xp:lib-i18n:${xpVersion}"
    include 'org.codehaus.jackson:jackson-mapper-asl:1.8.5'
    include 'org.apache.httpcomponents:httpclient:4.5.2'
}

task gulpLess( type: GulpTask, dependsOn: npmInstall ) {
    description = 'Build less resources.'
    args = ['css']
}

task cleanLessFiles( type: Delete ) {
    description = 'Clean source less files.'
    delete fileTree( dir: 'build/resources/main/site/assets/css' , excludes: ['*.css'])
}

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url 'http://repo.enonic.com/public'
    }
}

uploadArchives {
    repositories {
        mavenDeployer {
            repository( url: 'http://repo.enonic.com/artifactory/public' )
        }
    }
}

jar.dependsOn += gulpLess
build.dependsOn += cleanLessFiles